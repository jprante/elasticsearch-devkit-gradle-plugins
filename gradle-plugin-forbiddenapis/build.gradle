plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"

group = 'org.xbib.gradle.plugin'

repositories {
    jcenter()
}

dependencies {
    compileOnly gradleApi()
    compile "org.xbib.elasticsearch:forbiddenapis:${project.property('elasticsearch-libs.version')}"
    testCompile gradleTestKit()
    testCompile "org.spockframework:spock-core:${project.property('spock.version')}"
}

jar {
    manifest {
        attributes('Implementation-Version': project.version)
    }
}

test {
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

groovydoc {
    docTitle = description
    groovyClasspath = tasks.jar.outputs.files
    groovyClasspath += configurations.compile
    groovyClasspath += configurations.runtime
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, groovydocJar
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = 'https://github.com/jprante/elasticsearch-devkit-gradle-plugins/gradle-plugin-forbiddenapis'
        vcsUrl = 'https://github.com/jprante/elasticsearch-devkit-gradle-plugins/gradle-plugin-forbiddenapis'
        plugins {
            forbiddenApisPlugin {
                id = 'org.xbib.gradle.plugin.forbiddenapis'
                description = 'Gradle plugin for forbidden APIs'
                displayName = 'Gradle plugin for forbidden APIs'
                tags = ['xbib', 'groovy', 'java', 'apis', 'forbidden']
                version = project.version
            }
        }
    }
}

apply from: "${rootProject.projectDir}/gradle/publish.gradle"
