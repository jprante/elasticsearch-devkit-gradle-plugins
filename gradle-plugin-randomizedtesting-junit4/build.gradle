plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
}

dependencies {
    compileOnly gradleApi()
    compile "org.xbib.elasticsearch:randomizedtesting-junit4-ant:${project.property('elasticsearch-libs.version')}"
    testCompile gradleTestKit()
    testCompile "org.spockframework:spock-core:${project.property('spock.version')}"

}

jar {
    manifest {
        attributes('Implementation-Version': project.version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

groovydoc {
    docTitle = description
    groovyClasspath = tasks.jar.outputs.files
    groovyClasspath += configurations.compile
    groovyClasspath += configurations.runtime
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, groovydocJar
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = 'https://github.com/jprante/elasticsearch-devkit-gradle-plugins/gradle-plugin-randomizedtesting-junit4'
        vcsUrl = 'https://github.com/jprante/elasticsearch-devkit-gradle-plugins/gradle-plugin-randomizedtesting-junit4'
        plugins {
            randomizedTestingPlugin {
                id = 'org.xbib.gradle.plugin.randomizedtesting'
                description = 'Gradle plugin for randomized testing'
                displayName = 'Gradle plugin for randomized testing'
                tags = ['xbib', 'test', 'randomized']
                version = project.version
            }
        }
    }
}

apply from: "${rootProject.projectDir}/gradle/publish.gradle"
