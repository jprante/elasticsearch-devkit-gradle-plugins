plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"

dependencies {
    compileOnly gradleApi()
    compile project(':gradle-plugin-randomizedtesting-junit4')
    compile project(':gradle-plugin-forbiddenapis')
    compile "org.apache.rat:apache-rat:${project.property('apache-rat.version')}"
    compile "org.xbib.elasticsearch:jna:${project.property('elasticsearch-libs.version')}"
    compile "org.xbib.elasticsearch:log4j:${project.property('elasticsearch-libs.version')}"
    testCompile gradleTestKit()
    testCompile "org.spockframework:spock-core:${project.property('spock.version')}"

}

task injectGradleProperties(type: Copy) {
    from "${rootProject.projectDir}/gradle.properties"
    into 'build/resources/main'
    expand([
            'timestamp': new Date().format('yyyyMMddHHmmss')
    ])
}

compileGroovy.finalizedBy injectGradleProperties

jar {
    manifest {
        attributes('Implementation-Version': project.version)
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

groovydoc {
    docTitle = description
    groovyClasspath = files(tasks.jar.outputs.files, configurations.compile, configurations.runtime)
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar, groovydocJar
}

if (project.hasProperty('gradle.publish.key')) {
    pluginBundle {
        website = 'https://github.com/jprante/elasticsearch-devkit-gradle-plugins/gradle-plugin-elasticsearch-build'
        vcsUrl = 'https://github.com/jprante/elasticsearch-devkit-gradle-plugins/gradle-plugin-elasticsearch-build'
        plugins {
            esBuildPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.build'
                description = 'Gradle plugin for building Elasticsearch'
                displayName = 'Gradle plugin for building Elasticsearch'
                tags = ['xbib', 'elasticsearch', 'build']
                version = project.version
            }
            esDocsTestPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.docs-test'
                description = 'Gradle plugin for testing Elasticsearch docs'
                displayName = 'Gradle plugin for testing Elasticsearch docs'
                tags = ['xbib', 'elasticsearch', 'docs', 'test']
                version = project.version
            }
            esMetaPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.meta-plugin'
                description = 'Gradle plugin for meta-building Elasticsearch plugins'
                displayName = 'Gradle plugin for meta-building Elasticsearch plugins'
                tags = ['xbib', 'elasticsearch', 'plugin', 'build', 'meta']
                version = project.version
            }
            esPluginPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.plugin'
                description = 'Gradle plugin for building Elasticsearch plugins'
                displayName = 'Gradle plugin for building Elasticsearch plugins'
                tags = ['xbib', 'elasticsearch', 'plugin', 'build']
                version = project.version
            }
            esMessyTestPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.messy-test'
                description = 'Gradle plugin for messy-testing Elasticsearch'
                displayName = 'Gradle plugin for messy-testing Elasticsearch'
                tags = ['xbib', 'elasticsearch', 'test', 'messy']
                version = project.version
            }
            esRestTestPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.rest-test'
                description = 'Gradle plugin for REST-testing Elasticsearch'
                displayName = 'Gradle plugin for REST-testing Elasticsearch'
                tags = ['xbib', 'elasticsearch', 'test', 'rest']
                version = project.version
            }
            esStandaloneRestTestPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.standalone-rest-test'
                description = 'Gradle plugin for standalone REST-testing Elasticsearch'
                displayName = 'Gradle plugin for standalone REST-testing Elasticsearch'
                tags = ['xbib', 'elasticsearch', 'test', 'rest', 'standalone']
                version = project.version
            }
            esStandaloneTestPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.standalone-test'
                description = 'Gradle plugin for standalone testing Elasticsearch'
                displayName = 'Gradle plugin for standalone testing Elasticsearch'
                tags = ['xbib', 'elasticsearch', 'test', 'standalone']
                version = project.version
            }
            esTestWithDependenciesPlugin {
                id = 'org.xbib.gradle.plugin.elasticsearch.test-with-dependencies'
                description = 'Gradle plugin for testing Elasticsearch with dependencies'
                displayName = 'Gradle plugin for testing Elasticsearch with dependencies'
                tags = ['xbib', 'elasticsearch', 'test', 'dependencies']
                version = project.version
            }
        }
    }
}

apply from: "${rootProject.projectDir}/gradle/publish.gradle"
